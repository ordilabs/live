
setup:
  just create-bitcoin-conf
  just setup-hosts
  echo 'Now run `docker-compose up` in a permanent tab'

setup-part-2:
  just miner-create || just miner-load
  just generate-101
  just faucet
  just generate-1
  just faucet

clean:
  rm -r data

[linux]
install-tools:
  sudo apt install -y \
    imagemagick \
    webp
  
  cargo install --locked \
    sccache \
    cargo-leptos \

[macos]
install-tools:
  brew install \
    imagemagick \
    webp \
    sccache \

  cargo install --locked \
    cargo-leptos \


alias g := generate-1

bitcoin_conf := '''
regtest=1
[regtest]
 txindex=1
server=1
rpcuser=mempool
rpcpassword=mempool
rpcallowip=0/0
rpcbind=0.0.0.0
rpcport=18443
'''

create-bitcoin-conf:
  mkdir -p data/bitcoin && echo "{{bitcoin_conf}}" > data/bitcoin/bitcoin.conf
  printf '%s' "mempool:mempool" > data/bitcoin.cookie # needed for electrs

setup-hosts:
  just add-host-once mempool.ol
  just add-host-once ord.ol
  just add-host-once live.ol

add-host-once HOST:
  grep {{HOST}} /etc/hosts || echo "127.0.0.1 {{HOST}}" | sudo tee -a /etc/hosts

# docker-compose up

miner-create:
  docker-compose exec bitcoin-core bitcoin-cli -regtest createwallet miner

miner-load:
  docker-compose exec bitcoin-core bitcoin-cli -regtest loadwallet miner

enter-bitcoin-core: # enter
 docker-compose exec bitcoin-core bash

generate-101:
  docker-compose exec bitcoin-core bitcoin-cli -regtest -generate 101

generate-1: # generate
  docker-compose exec bitcoin-core bitcoin-cli -regtest -generate 1

faucet:
  docker-compose exec bitcoin-core bitcoin-cli -regtest \
    -rpcwallet=miner -named sendtoaddress \
      address=`docker-compose exec bitcoin-core bitcoin-cli -regtest getnewaddress` \
      amount=10 \
      fee_rate=1.1

punks:
    @echo Downloading, unpacking punks to tmp/punks
    mkdir -p /tmp/punks
    cd /tmp && curl -LO "https://github.com/larvalabs/cryptopunks/raw/master/punks.png"
    cd /tmp/punks && convert ../punks.png -crop 100x100@ +repage +adjoin punk_%d.png 
    cd /tmp/punks && ls *.png | xargs -n1 -I{} cwebp -lossless {} -o {}.webp
    