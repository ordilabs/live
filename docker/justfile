default:
  just --list

setup-once:
  sudo just _setup-hosts
  just _install-tools
  just _download-punks
  just open

up:
  docker-compose up

down:
  docker-compose down
  -rm -r data

ORDA := "ord -r --wallet alice --rpc-url bitcoin-core:18443/wallet/alice"
_fixtures:
  -[ -f .fixtures ] && sleep infinity
  -bitcoin-cli -named createwallet wallet_name=miner load_on_startup=true
  bitcoin-cli -rpcwallet=miner getnewaddress | tee miner.txt && just _generate 101
  -{{ORDA}} wallet restore "pipe minimum luggage modify solid sail another twenty say pioneer cloud scrub"
  [ -f alice.txt ] || {{ORDA}} wallet receive | jq -r .address | tee alice.txt
  bitcoin-cli -rpcwallet=miner -named sendtoaddress fee_rate=1 address=`cat alice.txt` amount=1
  bitcoin-cli -rpcwallet=miner -named sendtoaddress fee_rate=1 address=`cat alice.txt` amount=2
  bitcoin-cli -rpcwallet=miner -named sendtoaddress fee_rate=1 address=`cat alice.txt` amount=3
  bitcoin-cli -rpcwallet=miner -named sendtoaddress fee_rate=1 address=`cat alice.txt` amount=4
  just _generate 1
  {{ORDA}} wallet balance
  {{ORDA}} wallet inscribe `ls /tmp/punks/punk_0.png.webp | shuf | head -n1`
  {{ORDA}} wallet inscribe `ls /tmp/punks/punk_1.png.webp | shuf | head -n1`
  {{ORDA}} wallet inscribe `ls /tmp/punks/punk_2.png.webp | shuf | head -n1`
  {{ORDA}} wallet inscribe `ls /tmp/punks/punk_3.png.webp | shuf | head -n1`
  just _generate 1 
  touch .fixtures
  sleep infinity

_generate BLOCKS:
  bitcoin-cli -rpcwallet=miner -named generatetoaddress nblocks={{BLOCKS}} address=`cat miner.txt`

[macos]
open:
  open -a "Google Chrome" \
    http://live.ol \
    http://ord.ol \
    http://mempool.ol \


[linux]
_install-tools:
  sudo apt install -y \
    imagemagick \
    webp
  
  cargo install --locked \
    sccache \
    cargo-leptos \

[macos]
_install-tools:
  brew install \
    imagemagick \
    webp \
    sccache \

  cargo install --locked \
    cargo-leptos \

_setup-hosts:
  just _add-host-once mempool.ol
  just _add-host-once ord.ol
  just _add-host-once live.ol

_add-host-once HOST:
  grep {{HOST}} /etc/hosts || echo "127.0.0.1 {{HOST}}" | sudo tee -a /etc/hosts

alias e := enter
enter CONTAINER:
  docker-compose exec {{CONTAINER}} /bin/bash

alias g := generate
generate NBLOCKS:
  docker-compose exec fixtures just _generate {{NBLOCKS}}

alias f := faucet
faucet ADDRESS AMOUNT:
  docker-compose exec fixtures just _faucet {{ADDRESS}} {{AMOUNT}}

_faucet ADDRESS AMOUNT:
  bitcoin-cli -rpcwallet=miner -named sendtoaddress fee_rate=1 address={{ADDRESS}} amount={{AMOUNT}}


alias p := inscribe-punk-1
inscribe-punk-1: # generate
  docker-compose exec fixtures just _inscribe-punk 1

_inscribe-punk PUNK:
  seq 9999 | shuf | head -n1 | xargs -I{} {{ORDA}} wallet inscribe /tmp/punks/punk_{}.png.webp
  just _generate 1


_download-punks:
    @echo Downloading, unpacking punks to '/tmp/punks/punk_*.webp'
    mkdir -p /tmp/punks
    cd /tmp && [ -f punks.png ] || curl -LO "https://github.com/larvalabs/cryptopunks/raw/master/punks.png"
    cd /tmp/punks && [ -f punk_0.png.webp ] || ( \
      convert ../punks.png -crop 100x100@ +repage +adjoin punk_%d.png && \
      ls *.png | xargs -n1 -I{} cwebp -lossless {} -o {}.webp && \
      ls *.png | grep -v webp | xargs rm )
    